#!/usr/bin/perl

use strict;
use warnings;

use FindBin qw($Bin);
use lib "$Bin/../lib";

use Hadouken;

use Cwd ();
use File::Basename();
use Daemon::Control;
use Config::General;
use TryCatch;
use namespace::autoclean;

use Data::Printer alias => 'Dumper', colored => 1;

my $filedirname = File::Basename::dirname(Cwd::abs_path(__FILE__));

# Change into the current file directory
# so items in our config are relative from here.
chdir $filedirname;

my $config_flag = "--config=";

my $config_filename = '/etc/hadouken.conf';

if (my ($idx) = grep $ARGV[$_] =~ $config_flag, 0 .. $#ARGV) {
    my($flag,$cf) = split(/=/, $ARGV[$idx]);
    splice @ARGV,$idx,1; # Remove so it does not affect the Daemon.
    $config_filename = $cf;
}

unless(-e $config_filename && -r $config_filename) {
    die "Error reading configuration file";
}

my $conf = Config::General->new(-ForceArray => 1, -ConfigFile => $config_filename, -AutoTrue => 1);

my %config = $conf->getall;

#print Dumper(%config);
#exit(0);

my $log_filename = exists $config{log} ? Cwd::abs_path($config{log}) : '/var/log/hadouken.log';

my $pid_filename = exists $config{pid} ? Cwd::abs_path($config{pid}) : '/var/run/hadouken.pid';

my $rsakey_filename = exists $config{rsa_key_file} ? Cwd::abs_path($config{rsa_key_file}) : '';

if($rsakey_filename ne '') {
    # Make sure file exists and is readable by effective uid/gid.
    unless(-e $rsakey_filename && -r $rsakey_filename) {
        die "RSA key is specified but does not exist.";
    }
}

unless(exists $config{admin} && $config{server}) {
    die "Missing configuration values";
}

sub on_config_update {

}

my $cb = Hadouken->new_with_options(
    nick => 'hadouken',
    servers => [ $config{server} ],
    admin => $config{admin},
    config_hash => \%config,
    config_filename => $config_filename,
    conf_obj => $conf,
    rejoin_on_kick => 1,
    quote_limit => $config{quote_limit} || '2',
    safe_delay => $config{safe_delay} || '0.25',
    bitly_user_id => $config{bitly_user_id} || '', # To disable shortening, remove from config!
    bitly_api_key => $config{bitly_api_key} || '',
    private_rsa_key_filename => $rsakey_filename,
    private_rsa_key_password => $config{rsa_key_password} || '',
    blowfish_key => $config{blowfish_key} || 'hadoukeyletmein', # DEFAULT Blowfish key
    ownerdir => $filedirname,
    reconnect => $config{reconnect} || 1,
    reconnect_delay => $config{reconnect_delay} || 30,
    iface => $config{iface} || 'eth0',
);


my %daemon_args = ( 
    name        => "Hadouken",
    lsb_start   => '$syslog $remote_fs',
    lsb_stop    => '$syslog',
    lsb_sdesc   => 'Hadouke bot',
    lsb_desc    => 'Hadouken bot by dek',

    program => sub { $cb->start },

    help => "Specify --config=file to avoid using default config path\n\tDefault path is /etc/hadouken.conf\n",
    kill_timeout => 6,

    pid_file    => $pid_filename,
    stderr_file => $log_filename,
    stdout_file => $log_filename,

    fork        => 2,
    quiet       => 0,
);

$daemon_args{user} = $config{user} if(exists $config{user});
$daemon_args{group} = $config{group} if(exists $config{group});

my $daemon = Daemon::Control->new(%daemon_args);

my ($command) = @{$cb->extra_argv};

defined $command || die "No command specified";

my $exit_code;

if ($command eq 'stop') {
    $daemon->pretty_print("Shutting Down", "red") if(defined $cb->start_time && $cb->start_time ne '');

    $cb->stop(); # Clean disconnect.

    $exit_code = $daemon->run_command('stop');
    exit($exit_code || 0);
}

try {
    $exit_code = $daemon->run_command($command);
}
catch (Str $e where { $_ =~ /^Error: undefined action/i } ) {
    warn "You must specify an action.\n";
}
catch($e) {
    warn $e,"\n";
}

exit(($exit_code || 0));


